include(CTest)
include(Catch)

set(TARGET MethaneGraphicsCameraTest)

add_executable(${TARGET}
    Main.cpp
    ArcBallCameraTest.cpp
)

target_precompile_headers(${TARGET} REUSE_FROM MethanePrecompiledHeaders)

target_link_libraries(${TARGET}
    PRIVATE
        MethaneGraphicsCamera
        MethaneBuildOptions
        MethanePrecompiledHeaders
        MethaneTestsCatchHelpers
        $<$<BOOL:${METHANE_TRACY_PROFILING_ENABLED}>:TracyClient>
        Catch2
)

catch_discover_tests(${TARGET})

if (METHANE_RUN_TESTS_DURING_BUILD)
    # Get list of CTest include files which have to be generated by catch_discover_tests before tests can be started
    get_directory_property(CTEST_INCLUDE_FILES "TEST_INCLUDE_FILES")
    add_custom_command(TARGET ${TARGET} POST_BUILD
        DEPENDS ${CTEST_INCLUDE_FILES}
        COMMAND ${CTEST_EXE} -C $<CONFIGURATION> --output-on-failure
    )
endif()

set_target_properties(${TARGET}
    PROPERTIES
    FOLDER Tests
)

install(TARGETS ${TARGET}
    RUNTIME
        DESTINATION Tests
        COMPONENT Test
)
